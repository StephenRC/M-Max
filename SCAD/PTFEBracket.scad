///////////////////////////////////////////////////////////////////////////////////////////////////////////
// PTFEBracket.scad
//////////////////////////////////////////////////////////////////////////////////////////////////////////
// Created 11/19/20
// Last Update 11/19/20
///////////////////////////////////////////////////////////////////////////////////////////////////////////
$fn=100;
include <bosl2/std.scad>
include <inc/brassinserts.scad>
/////////////////////////////////////////////////////////////////////////////////////////////////////////////
Thickness=5;
Clearance=0.5;
PTFEDiameter=4.12+Clearance;
Offsets=20;
LayerThickness=0.3;
///////////////////////////////////////////////////////////////////////////////////////////////////////////

Bracket(40,9.5,0); // M-Max:40

////////////////////////////////////////////////////////////////////////////////////////////////////////////

module Bracket(Length=40,Size=9.5,Top=0) {
	difference() {
		color("cyan") cuboid([Length+Offsets,20,Thickness],rounding=2,p1=[0,0]);
		translate([10,10,-2]) ExtrusionMount(screw5,Top);
		translate([Length+Offsets-10,10,-2]) cylinder(h=10,d=PTFEDiameter);
	}
	translate([Length+Offsets-10,10,0]) ConnectorMount(Size);
	CSSupport(Top);
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////

module ConnectorMount(Size=9.5) {
	difference() {
		color("green") cylinder(h=10,d=Size*1.5);
		translate([0,0,-2]) color("black") cylinder(h=15,d=Size);
		translate([0,0,14]) color("gray") hull() { // bevel edge to make it easier to insert fitting
			cylinder(h=1,d=Size*1.5);
			translate([0,0,-5])cylinder(h=1,d=Size);
		}
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////

module ExtrusionMount(Screw=screw5,Top=0) {
	color("red") cylinder(h=10,d=Screw);
	if(Top) {
			if(Screw==screw5) translate([0,0,6.5]) color("plum") cylinder(h=5,d=screw5hd);
			else if(Screw==screw3) translate([0,0,6.5]) color("plum") cylinder(h=5,d=screw3hd);
		} else {
			if(Screw==screw5) translate([0,0,-2.5]) color("plum") cylinder(h=5,d=screw5hd);
			else if(Screw==screw3) translate([0,0,-2.5]) color("plum") cylinder(h=5,d=screw3hd);
		}
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////

module CSSupport(Top=0) {
	if(!Top)
		translate([10,10,0.5]) color("black") cylinder(h=LayerThickness,d=screw5hd);
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////