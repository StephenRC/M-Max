///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Spool_Sount.scad - mount a spool onto 2020
// created: 1/27/2019
// last modified: 1/27/19
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// 1/27/19	- Mounting a spool to 2020, uses 4 M3x8mm screws, 4 M3 post install nuts, 200mm of 8mm threaded rod,
//			  three M8 nuts, two M8 washers, 20mm of 2020, one M5 insall nut, one M5x5mm or M5x25mm cap screw
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// uses http://www.thingiverse.com/thing:1647748 to hold the spool
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
include <inc/cubex.scad>
include <inc/screwsizes.scad>
$fn=100;
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
clearance = 0.4;
w2020 = 20 + clearance;
outer_w = 28;
length = 60;
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//spool();
//translate([-10,10,0])
	spacer(70); // fill in 8mm rod, so you don't have to screw the nut on/off for what seems like forever


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

module spool() {
	frame_mount();
	rod_mount();
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

module rod_mount() {
	difference() {
		translate([length/2,outer_w,outer_w]) rotate([90,0,0]) color("cyan") cylinder(h=outer_w,d=30);
		spool_screw_hole();
		notch_2020();
	}
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

module spool_screw_hole() {
	translate([length/2,outer_w+2,outer_w]) rotate([90,0,0]) color("blue") cylinder(h=outer_w+5,d=screw8);
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


module frame_mount() {
	difference() {
		color("lightgrey") cubeX([length,outer_w,outer_w],2); 
		notch_2020();
		spool_screw_hole();
		mount_2020();
	}
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

module mount_2020() {
	translate([15,outer_w+3,10]) color("plum") rotate([90,0,0]) cylinder(h=outer_w+5,d=screw3);
	translate([45,outer_w+3,10]) color("yellow") rotate([90,0,0]) cylinder(h=outer_w+5,d=screw3);
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

module notch_2020() {
	translate([-2,(outer_w-w2020)/2,-5+clearance]) color("red") cube([length+5,w2020,25],2);
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////

module spacer(Len=10) {
	difference() {
		color("blue") cylinder(h=Len,d=screw8+4.5);
		translate([0,0,-5]) color("red") cylinder(h=Len+10,d=screw8+1);
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
